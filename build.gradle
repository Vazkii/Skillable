buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: "maven"
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "${mc_version}-" + getVersion()
group = "codersafterdark.reskillable" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${mod_name}"

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    coreMod = "codersafterdark.reskillable.base.asm.LoadingPlugin"

    mappings = "${mc_mappings}"
    replace 'GRADLE:VERSION', "${mc_version}"

    // Scala: not even once
    replace 'scala.actors.threadpool.Arrays', 'java.util.Arrays'

    makeObfSourceJar = false
}

repositories{
    maven{
        name 'CraftTweaker'
        url 'http://maven.blamejared.com'
    }
    maven{
        name 'JEI'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name = 'Waila'
        url 'https://tehnut.info/maven'
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12.2:4.8.5.144"
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.21-B36_1.12"
    //deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.6.465"
    //deobfCompile "CraftTweaker2:CraftTweaker2-API:1.12-4.1.6.465"
    //deobfCompile "CraftTweaker2:ZenScript:1.12-4.1.6.465"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}

jar {
    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.exe"
    exclude "**/unused"

    manifest {
        attributes("FMLCorePluginContainsFMLMod": "true", "FMLCorePlugin": "codersafterdark.reskillable.base.asm.LoadingPlugin")
        attributes "FMLAT": "reskillable_at.cfg"
    }

}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': "${mc_version}"
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', '**/psd/**'
    }
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives srcJar
}

String getVersion() {
    String version = "${mod_version}"
    String branch = project.hasProperty("branch") ? project.branch : ""

    if(branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if(buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if(buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? "." + System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}


